%import common.INT
%import common.SIGNED_NUMBER
%import common.ESCAPED_STRING
%import common.CNAME
%import common.CPP_COMMENT
%import common.WS
%ignore WS
%ignore CPP_COMMENT

r_tag_in: "in"
r_tag_out: "out"
r_tag_var: "var"
r_tag_block: "block"
r_tag_module: "module"
_var_tag: r_tag_in | r_tag_out | r_tag_var
_state_tag: r_tag_block | r_tag_module

r_constant: CNAME "(" INT ")"
r_reference: CNAME ("." CNAME)*
_expression: r_reference | r_op | r_constant

r_var_size: ["[" INT "]"]
r_var_expr: ["=" _expression]
r_var: _var_tag CNAME CNAME r_var_size r_var_expr ";"

r_op_args: _expression ("," _expression)*
r_op: CNAME "(" r_op_args ")"

r_state_connection: "." CNAME "=" _expression
r_state_expr: "=" "{" r_state_connection ("," r_state_connection)* "}" ";"
r_state: _state_tag CNAME CNAME r_state_expr

_state_decl_item: r_var | r_state
r_state_decl_items: "{" _state_decl_item+ "}"
r_state_decl: _state_tag CNAME r_state_decl_items ";"

start: r_state_decl+
